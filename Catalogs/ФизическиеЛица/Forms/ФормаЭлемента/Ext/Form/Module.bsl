&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если Объект.ИмяФайлаКартинки = "" Тогда
		КартинкаВременноеХранилище = ПолучитьНавигационнуюСсылку(Объект.Ссылка,"КартинкаХранилище");
	Иначе
		Попытка
			Обработчик = Новый ОписаниеОповещения("ЗагрузитьСДискаЗавершение", ЭтотОбъект);
			НачатьПомещениеФайла(Обработчик,,Объект.ИмяФайлаКартинки,Ложь,УникальныйИдентификатор);
		Исключение
			Сообщить("Не найден файл фото по адресу: " + Объект.ИмяФайлаКартинки);
		КонецПопытки;
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если ТекущийОбъект.ХранитьВБазе Тогда
		Если ЭтоАдресВременногоХранилища(КартинкаВременноеХранилище) Тогда
			ДвоичныеДанные =  ПолучитьИзВременногоХранилища(КартинкаВременноеХранилище);
			ТекущийОбъект.ИмяФайлаКартинки = "";
			ТекущийОбъект.КартинкаХранилище = Новый ХранилищеЗначения(ДвоичныеДанные, Новый СжатиеДанных(9));
		КонецЕсли; 
	Иначе
	    ТекущийОбъект.ИмяФайлаКартинки = ИмяФайла;
		ТекущийОбъект.КартинкаХранилище = Неопределено;
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСДиска(Команда)
	КартинкаВременноеХранилище ="";
	Обработчик = Новый ОписаниеОповещения("ЗагрузитьСДискаЗавершение", ЭтотОбъект);
	НачатьПомещениеФайла(Обработчик,,,Истина,УникальныйИдентификатор);
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСДискаЗавершение(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	Если Результат Тогда
		КартинкаВременноеХранилище = Адрес;
		ИмяФайла = ВыбранноеИмяФайла;
		Если Не ИмяФайла=Объект.ИмяФайлаКартинки Тогда
			Модифицированность = Истина;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
